SUB GetAllScripts(_connection1, _connection2, _folder)

/*
needs two connection to Google Drive
- Google Drive & Spreadsheets connection to read meta data about the Google Drive (file list/folder list)
- Google Drive connection to extract the text files

All *.qvs files found are put into variables vScript1, vScript2 ... vScriptN (alphabetically sorted)
and a variable vAllScripts is set to call the vScripts. 

So to execute script from Google Drive Folder do the following 2 commands:

CALL GetAllScripts('GDrive_meta_connection', 'GDrive_files_connection', 'MyScriptFolder');
$(vAllScripts);

One script file must not exceed 64kB

*/
	LIB CONNECT TO '$(_connection1)';
    [~gDriveContent]: 
    LOAD *, If(mimeType LIKE '*folder*' AND (title LIKE '$(_folder)' OR id LIKE '$(_folder)'), id) AS folderId;
    SELECT title, id, mimeType, parents_id FROM ListFiles;

    IF FieldValueCount('folderId') = 0 THEN 
        [Folder $(_folder) not found in connection $(_connection1)];
    ELSEIF FieldValueCount('folderId') > 1 THEN 
        [More than one folder $(_folder) found in connection $(_connection1)];
    ELSE
        LET _folder = FieldValue('folderId', 1);
        LET vAllScripts = '';
        INNER JOIN ([~gDriveContent])
        LOAD '$(_folder)' AS parents_id AUTOGENERATE(1);
        
        [~scriptFiles]: 
        LOAD title AS [~scriptFileName] 
        RESIDENT [~gDriveContent] 
        WHERE title LIKE '*.qvs' 
        ORDER BY title ASC;
        
        FOR v_GAS_i = 1 TO FieldValueCount('~scriptFileName')
            LET v_GAS_FileName = FieldValue('~scriptFileName', v_GAS_i);
            
            reading:
            LOAD Concat(@1,CHR(10),RecNo()) AS [$script]
            FROM [lib://$(_connection2)/$(_folder)/$(v_GAS_FileName)]
            (txt, utf8, no labels, delimiter is '\n', no quotes);
            
            LET vScript$(v_GAS_i) = Peek('$script');
            DROP TABLE reading;
            
            LET vAllScripts = vAllScripts & '$' & '(vScript$(v_GAS_i));' & CHR(10)
            	& 'LET vScript$(v_GAS_i) = Null();' & CHR(10);
        NEXT v_GAS_i
        
        TRACE GetAllScripts found $(v_GAS_i) *.qvs files in folder $(_folder);
        
        LET v_GAS_i = Null();
        LET v_GAS_FileName = Null();
    END IF
    DROP TABLES [~scriptFiles], [~gDriveContent];
END SUB
