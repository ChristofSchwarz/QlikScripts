  TRACE SUB CreatePrevNextVar($(paramField), $(paramType), $(paramPrecision));
  /*
  This sub will create 4 variables for a given fieldname (provided in paramField). The
  paramType is one of the following: Txt or Text, Num or Numeric, Dual
  The optional 3rd parameter is Precision and is needed for numeric or dual values
  with lots of decimals (that is the case for timestamps for example!) and it defines
  many digits after the decimal need to be identical for a match (e.g. 1e-8 or 1e-9)
  */
  
  IF paramType LIKE 'T*xt' THEN

    LET [vNext:$(paramField)] = 'If(IsNull([$(paramField)]), MinString([$(paramField)]),
      MinString(TOTAL {<[$(paramField)]={"=Only({<[$(paramField)]=>}[$(paramField)]) > Only(TOTAL [$(paramField)])"}>} [$(paramField)])
    )';
    LET [vNext:$(paramField)] = CHR(39) & '=Only({<[$(paramField)]=>}[$(paramField)])=''''$' & '(=' & [vNext:$(paramField)] & ')''''' & CHR(39);    
    LET [vNextVal:$(paramField)] = 'If(IsNull([$(paramField)]), MinString([$(paramField)]),
      MinString(TOTAL {<[$(paramField)]={"=Only({<[$(paramField)]=>}[$(paramField)]) > Only(TOTAL [$(paramField)])"}>} [$(paramField)])
    )';
    
    LET [vPrev:$(paramField)] = 'If(IsNull([$(paramField)]), MaxString([$(paramField)]),
      MaxString(TOTAL {<[$(paramField)]={"=Only({<[$(paramField)]=>}[$(paramField)]) < Only(TOTAL [$(paramField)])"}>} [$(paramField)])
    )';
    LET [vPrev:$(paramField)] = CHR(39) & '=Only({<[$(paramField)]=>}[$(paramField)])=''''$' & '(=' & [vPrev:$(paramField)] & ')''''' & CHR(39);    ;
    LET [vPrevVal:$(paramField)] = 'If(IsNull([$(paramField)]), MaxString([$(paramField)]),
      MaxString(TOTAL {<[$(paramField)]={"=Only({<[$(paramField)]=>}[$(paramField)]) < Only(TOTAL [$(paramField)])"}>} [$(paramField)])
    )';
    
  ELSEIF Wildmatch(paramType, 'Num*', 'Dual') THEN
  
    LET [vNextVal:$(paramField)] = 'If(IsNull([$(paramField)]), MinString([$(paramField)]),
      MinString(TOTAL {<[$(paramField)]={"=Only({<[$(paramField)]=>}[$(paramField)]) > Only(TOTAL [$(paramField)])"}>} [$(paramField)])
    )';
    LET [vPrevVal:$(paramField)] = 'If(IsNull([$(paramField)]), MaxString([$(paramField)]),
      MaxString(TOTAL {<[$(paramField)]={"=Only({<[$(paramField)]=>}[$(paramField)]) < Only(TOTAL [$(paramField)])"}>} [$(paramField)])
    )';
    LET [vNext:$(paramField)] = 'Num(If(IsNull([$(paramField)]), Min([$(paramField)]),
      Min(TOTAL {<[$(paramField)]={"=Only({<[$(paramField)]=>}[$(paramField)]) > Only(TOTAL [$(paramField)])"}>} [$(paramField)])
    ),'''',''.'','' '')';
    LET [vPrev:$(paramField)] = 'Num(If(IsNull([$(paramField)]), Max([$(paramField)]),
      Max(TOTAL {<[$(paramField)]={"=Only({<[$(paramField)]=>}[$(paramField)]) < Only(TOTAL [$(paramField)])"}>} [$(paramField)])
    ),'''',''.'','' '')';
    IF '$(paramPrecision)' = '' OR '$(paramPrecision)' = '0' THEN
      LET [vNext:$(paramField)] = CHR(39) & '=Only({<[$(paramField)]=>}[$(paramField)]) = $' & '(=' & [vNext:$(paramField)] & ')' & CHR(39);
      LET [vPrev:$(paramField)] = CHR(39) & '=Only({<[$(paramField)]=>}[$(paramField)]) = $' & '(=' & [vPrev:$(paramField)] & ')' & CHR(39);
    ELSE
      LET [vNext:$(paramField)] = CHR(39) & '=FAbs(Only({<[$(paramField)]=>}[$(paramField)]) - $' & '(=' & [vNext:$(paramField)] & ')) < ' 
      & Num(paramPrecision,'','.',' ') & CHR(39);
      LET [vPrev:$(paramField)] = CHR(39) & '=FAbs(Only({<[$(paramField)]=>}[$(paramField)]) - $' & '(=' & [vPrev:$(paramField)] & ')) < '
      & Num(paramPrecision,'','.',' ') & CHR(39);
    END IF
 
  ELSE
     // Throw error
     [SUB CreatePrevNextVar: Unknown Field type $(paramType), only Text and Num allowed];

  END IF

END SUB
